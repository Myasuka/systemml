# The Monte Carlo Method (see http://en.wikipedia.org/wiki/Monte_Carlo_method)
# A number of random points is distributed inside a square
# with a width and height of 1. For each point the algorithm checks if
# it is inside a circle of radius 1 that fits into the square.
# In the end the ratio of points inside the circle is calculated
# and multiplied by 4 since the ratio of the area inside and outside of the
# circle is pi/4.
# Ain / A = pi / 4 => pi = Ain / A * 4

# Number of points to be generated
n = 5000000;

# Generate n random points
R = Rand(rows=n, cols=2, min=0, max=1, pdf="uniform", sparsity=1.0);

# Calculate sqrt(x^2 + y^2) for each point
R = R^2;
MC = rowSums(R);
MC = sqrt(MC);

# Check if the result is smaller or equal than 1
# If it is, it is a "hit", the point is in the circle
RES = ppred(MC, 1, "<=");

# Count the number of "hits"
T = sum(RES);

# Calculate PI
pi = 4 * T / n;

print(pi);
