# Univariate statistics for scale variable
# (with weights)

V = readMM("in/salary.ordinal.data", rows=474, cols=1, format="text");
W = readMM("in/jobcat.ordinal.data", rows=474, cols=1, format="text");

# vector that has required quantiles
P = readMM("in/prob", rows=5, cols=1, format="text")

W = round(W)

n = nrow(V)

wt=sum(W)

# sum
s1 = sum(V*W)

# squared sum
s2 = sum(V^2*W)

# cubic sum
s3 = sum(V^3*W)

# quard sum
s4 = sum(V^4*W)

# mean
mu = s1/wt

# standard deviations
std_dev = sqrt((s2/wt - mu*mu)*(wt/(wt-1)))

# standard errors of mean
SE = std_dev/sqrt(wt)

# variances
var = std_dev^2

# coefficients of variation
cv = std_dev/mu

# harmonic means (note: may generate out of memory for large sparse matrices becauses of NaNs)
har_mu = wt/(sum((1.0/V)*W))

# geometric means is not currently supported.
# geom_mu = n*exp(sum(log(V))/n)

# min and max
mn=min(V)
mx=max(V)

# range
rng = mx - mn

# 2nd central moment
m2=var*(wt-1)/wt

# 4th central moment
m4=(s4-4*mu*s3+6*mu^2*s2-4*mu^3*s1)/wt+mu^4

# Skewness
g1 = (s3 - 3*mu*s2 + 3*mu^2*s1 -wt*mu^3)*wt/((wt-1)*(wt-2)*std_dev^3)

# standard error of skewness
se_g1=sqrt( 6*wt*(wt-1) / ((wt-2)*(wt+1)*(wt+3)) )

# Kurtosis (using binomial formula)
#g2 = (sum(V^4) - 4*s3*mu + 6*s2*mu^2 - 3*n*mu^4)/(n*std_dev^4) - 3
g2=(wt^2*(wt+1)*m4-3*wt^2*(wt-1)*m2^2)/((wt-1)*(wt-2)*(wt-3)*std_dev^4)

# Standard error of Kurtosis
se_g2= sqrt( (4*(wt^2-1)*se_g1^2)/((wt+5)*(wt-3)) )

# outliers use ppred to describe it
out_minus = ppred(V, mu-5*std_dev, "<")*V 
out_plus = ppred(V, mu+5*std_dev, ">")*V

# median
md = quantile(V, W, 0.5)

# quantile
Q = quantile(V, W, P)

# inter-quartile mean
iqm = interQuartileMean(V, W)

print("mean = ", mu );
print("standard dev. = ", std_dev );
print("standard error = ", SE );
print("variance = ", var );
print("coe. of variation = ", cv );
print("harmonic mean = ", har_mu );
#print("geometric mean = ", geom_mu );
print("min = ", mn );
print("max = ", mx );
print("range = ", rng );
print("skewness = ", g1 );
print("standard error in skewness = ", se_g1 );
print("kurtosis = ", g2 );
print("standard error in kurtosis = ", se_g2 );
print("meadian = ", md );
print("inter-quartile mean = ", iqm );

writeMM(out_minus, "out/out_minus", format="text");
writeMM(out_plus, "out/out_plus", format="text");
writeMM(Q, "out/quantile", format="text");

