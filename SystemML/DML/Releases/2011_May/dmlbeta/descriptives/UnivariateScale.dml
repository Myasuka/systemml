$$readhelper$$

V = read("in/salary.ordinal.data", rows=474, cols=1, format="text");
# vector that has required quantiles
P = read("in/prob", rows=5, cols=1, format="text")

n = nrow(V)

# sum
s1 = sum(V)

# squared sum
s2 = sum(V^2)

# cubic sum
s3 = sum(V^3)

# quard sum
s4 = sum(V^4)

# mean
mu = s1/n

# standard deviations
std_dev = sqrt((s2/n - mu*mu)*(n/(n-1)))

# standard errors of mean
SE = std_dev/sqrt(n)

# variances
var = std_dev^2

# coefficients of variation
cv = std_dev/mu

# harmonic means (note: may generate out of memory for large sparse matrices becauses of NaNs)
har_mu = n/sum((1.0/V))

# geometric means is not currently supported.
# geom_mu = n*exp(sum(log(V))/n)

# min and max
mn=min(V)
mx=max(V)

# range
rng = mx - mn

# 2nd central moment
m2=var*(n-1)/n

# 4th central moment
m4=(s4-4*mu*s3+6*mu^2*s2-4*mu^3*s1)/n+mu^4

# Skewness
g1 = (s3 - 3*mu*s2 + 3*mu^2*s1 -n*mu^3)/(n*std_dev^3)

# standard error of skewness (not sure how it is defined without the weight)
se_g1=sqrt( 6*n*(n-1.0) / ((n-2.0)*(n+1.0)*(n+3.0)) )

# Kurtosis (using binomial formula)
g2 = (sum(V^4) - 4*s3*mu + 6*s2*mu^2 - 3*n*mu^4)/(n*std_dev^4) - 3

# Standard error of Kurtosis (not sure how it is defined without the weight)
se_g2= sqrt( (4*(n^2-1)*se_g1^2)/((n+5.0)*(n-3.0)) )

# median
md = quantile(V, 0.5)

# quantile
Q = quantile(V, P)

# inter-quartile mean
iqm = interQuartileMean(V)

# outliers use ppred to describe it
out_minus = ppred(V, mu-5*std_dev, "<")*V 
out_plus = ppred(V, mu+5*std_dev, ">")*V

print("mean = ", mu );
print("standard dev. = ", std_dev );
print("standard error = ", SE );
print("variance = ", var );
print("coe. of variation = ", cv );
print("harmonic mean = ", har_mu );
#print("geometric mean = ", geom_mu );
print("min = ", mn );
print("max = ", mx );
print("range = ", rng );
print("skewness = ", g1 );
print("standard error in skewness = ", se_g1 );
print("kurtosis = ", g2 );
print("standard error in kurtosis = ", se_g2 );
print("meadian = ", md );
print("inter-quartile mean = ", iqm );

write(out_minus, "out/out_minus", format="text");
write(out_plus, "out/out_plus", format="text");
write(Q, "out/quantile", format="text");
