#-------------------------------------------------------------
# IBM Confidential
# OCO Source Materials
# (C) Copyright IBM Corp. 2010, 2015
# The source code for this program is not published or
# otherwise divested of its trade secrets, irrespective of
# what has been deposited with the U.S. Copyright Office.
#-------------------------------------------------------------
#  
# THIS SCRIPT COMPUTES THE RATING/SCORE FOR A GIVEN LIST OF PAIRS: (USER-ID, ITEM-ID) USING 2 FACTOR MATRICES L AND R
# WE ASSUME THAT ALL USERS HAVE RATED AT LEAST ONCE AND ALL ITEMS HAVE BEEN RATED AT LEAST ONCE.
# INPUT   PARAMETERS:
# ---------------------------------------------------------------------------------------------
# NAME    TYPE     DEFAULT  MEANING
# ---------------------------------------------------------------------------------------------
# X       String   ---      The input user-ids list
# Y	 	  String   ---	    The output user-id x top-rating
# L       String   ---      Location of factor matrix L: user-id x feature-id 
# R       String   ---      Location of factor matrix R: feature-id x item-id
# V	  	  String   ---      Location of original matrix V: user-id x item-id
# K	  	  Int      5	    The number of top K items	
# fmt     String   "text"   The output format of the factor matrix user-id/item-id/score
# hadoop jar system-ml.jar -f ALS_topk_predict.dml -nvargs V=OUTPUT_DIR/V.mtx  X=OUTPUT_DIR/topk_recommend.mtx  
# L=OUTPUT_DIR/L.mtx  R=OUTPUT_DIR/R.mtx  Y=OUTPUT_DIR/predict.mtx fmt=csv

fileX      = $X;
fileY 	   = $Y;
fileL	   = $L;
fileR      = $R;
fileV	   = $V;
K	  	   = ifdef ($K, 5);
fmtO       = ifdef ($fmt, "text");    # $fmt="text";


X = read (fileX);
L = read (fileL);
R = read (fileR);
V = read (fileV);

Vrows = nrow(V);
Vcols = ncol(V);

K = min(Vcols,K);

n = nrow(X);

Lrows = nrow(L);
Rcols = ncol(R);

X_user_max = max(X[,1]);

if ( Lrows < Vrows | Rcols <  Vcols |  X_user_max > Vrows ){
       stop("Predictions can be provided only if the number of rows and columns of the original matrix have been retained after the factorization and the users
        in test data are also available in the factor matrices");
}

# creats projection matrix to select users
s = seq(1,nrow(X[,1]));
projection_matrix = table(s, X[,1],nrow(X),nrow(L));

# selects users from factor L
U_prime = projection_matrix %*% L;

# calculates V_filter for selected users
V_filter = U_prime %*% R;

# selects users from original V
V_prime = projection_matrix %*% V;

# filter for already recommended items
V_prime = ppred(V_prime,0,'==');

# removes already recommended items and creating user2item matrix
V_filter = V_prime * V_filter; 


#stores sorted movies for selected users 
V_top_indices = matrix(0, rows=nrow(V_filter), cols=K);
V_top_values = matrix(0, rows=nrow(V_filter), cols=K);

# a very large number to mask the max ratings
LARGE_VALUE = 999999;

# uses rowIndexMax/rowMaxs to update kth ratings
for (i in 1:K){
	rowIndexMax = rowIndexMax(V_filter);
	rowMaxs     = rowMaxs(V_filter);
	V_top_indices[,i] = rowIndexMax;
	V_top_values[,i] = rowMaxs;
	
	# projection_matrix to mask the indices with very large negative number
	projection_matrix = (1 - LARGE_VALUE*table(seq(1,nrow(V_filter),1),rowIndexMax,nrow(V_filter),ncol(V_filter)));
	
	#applying the mask to V_filter
	V_filter = V_filter * projection_matrix;
}

# append users as a first column
V_top_indices = append(X[,1],V_top_indices);
V_top_values = append(X[,1],V_top_values);

#writing top k elements
write(V_top_indices, fileY, format=fmtO);
write(V_top_values, fileY+".ratings", format=fmtO);



### ---  Old logic for selecting top k ratings  ----- 
# stores sorted items for selected users 
#V_top_indices = matrix(0, rows=ncol(V_filter), cols=nrow(V_filter));
#V_top_values = matrix(0, rows=ncol(V_filter), cols=nrow(V_filter));

#parfor (i in 1:ncol(V_filter)){
#	V_top_indices[i,] = t(order(target=V_filter[,i], decreasing=TRUE, index.return=TRUE));
#	V_top_values[i,] = t(order(target=V_filter[,i], decreasing=TRUE, index.return=FALSE));
#}

#selecting top K
#V_top_indices = V_top_indices[,1:K];
#V_top_values = V_top_values[,1:K];
