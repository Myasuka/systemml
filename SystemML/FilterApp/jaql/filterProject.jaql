///////////////////////////////////////////////////////////////////////////////
//
// IBM Confidential
//
// OCO Source Materials
//
// (C) Copyright IBM Corp. 2009, 2010, 2011, 2012
//
// The source code for this program is not published or
//
// otherwise divested of its trade secrets, irrespective of
//
// what has been deposited with the U.S. Copyright Office.
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// FILE filterProject.jaql
//
// DESCRIPTION: The jaql file that is called from oozie workflow.xml
///////////////////////////////////////////////////////////////////////////////

// -------------------------------------------------------------
// this script is used to perform Filter/Projection of input
// data in Json text format
// Input:
//        input Data File Name (JSON file stored on HDFS)
//        Meta Data File Name (JSON file stored on HDFS),
//        Filter expression ,
//        Projection List File Name(Json file stored on HDFS),
//        Output Meta Data File Name (JSON file stored on HDFS),
//        Output Data File Name (JSON file stored on HDFS)
// Output: Updated Meta Data file (JSON file stored on HDFS)
//         Updated Data File (JSON file stored on HDFS)
// ---------------------------------------------------------------


// register eval function to parse the meta data

addClassPath("lib/metaDataParse.jar");

eval = javaudf("com.ibm.systemML.dataTransformation.metaDataParse");

// ---------------------------------------------------------------------------
// Func Name:  jsonLines
//             Function to Read in the input data
// input params:
//           input: input data file name
// ---------------------------------------------------------------------------

jsonLines = fn(location)
    lines ( location,
        inoptions = { converter:
                     "com.ibm.jaql.io.hadoop.converter.FromJsonTextConverter"},
        outoptions = { converter:
                     "com.ibm.jaql.io.hadoop.converter.ToJsonTextConverter"});

// ---------------------------------------------------------------
// Perform Filtering, Projecting on the input data
// ---------------------------------------------------------------
/*
Input_data_PARAM = "./data/inputdata.json";
Input_metaData_PARAM = "./data/meta.json";
Input_filterExpression_PARAM ="$.GPA > 3.6 and $.Salary > 6000";
Input_projectColumns_PARAM ="./data/projListFile.json";
Input_outMetaData_PARAM = "./data/meta-update.json";
Input_outData_PARAM = "./data/inputData-update.json";
*/

// pin in meta data in memory
metaDataMem := read (jsonText(Input_metaData_PARAM));

// read in input data
inputData = read(jsonLines(Input_data_PARAM));

// e.g: Input_filterExpression_PARAM= '$.GPA > 3.6 and $.Salary > 6000';
filterFunc = if (isnull(Input_filterExpression_PARAM)) null
                else eval('fn($) ' + Input_filterExpression_PARAM);

// filter input data
inputData = if (not isnull(filterFunc)) (
                 inputData-> filter each e filterFunc(e)
              ) else inputData;

// read in projection column file
projListFile := if (not isnull (Input_projectColumns_PARAM)) (
                  read (jsonText(Input_projectColumns_PARAM))
                ) else null;
// get projection columns if there exists one
projectColumns = if (not isnull (projListFile)) (
                   projListFile -> transform $.projectingColumns 
                                -> expand
                 ) else null;

// if it is null, take the filtering out column list and
// convert it to the actual project list
projectColumns = if (isnull(projectColumns)) (
                   if (not isnull(projListFile)) (
                      filteringOutColumns = projListFile
                                        -> transform $.filteringOutColumns
                                        -> expand,
                      if ( not isnull(filteringOutColumns)) (
                        metaDataMem -> filter not $.attributeName in
                                        filteringOutColumns
                                 -> transform $.attributeName
                      ) else null
                   ) else null
                 ) else projectColumns;

// generate projectionList String
projectColumnStr = if (not isnull(projectColumns)) (
                   //write out the updated meta Data
                   metaDataMem = metaDataMem -> filter $.attributeName in
                                                projectColumns
                               -> transform each e { e{*-.attributeColumnId}}
                               -> enumerate()
                               -> transform {"attributeColumnId":$[0]+1, $[1]}
                               -> write (jsonText (Input_outMetaData_PARAM)),
                   projectColumns -> strJoin(",.")
                   ) else null;

// build the projection func
projectionFunc = if( not isnull(projectColumnStr)) (
                      eval ('fn (r) r{.' + projectColumnStr + '}')
                   )
                   else null;

// project input data
inputData = if (not isnull(projectionFunc)) (
               inputData
               -> transform each r projectionFunc(r)
              ) else inputData;

// output the data if done projection or filtering 
if ( (not isnull(projectionFunc)) or (not isnull(filterFunc))) (
               inputData -> write(jsonLines(Input_outData_PARAM))
);
